// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        compile_sdk_version = 28
        build_tools_version = '28.0.3'
        target_sdk_version = 28

        // variable for ijkplayer
        ijkplayer_version_code = 800800

        versions = [
                'kotlin_plugin'      : '1.2.71',
                'kotlin_env'         : 'jdk8',
                'android_plugin'     : '3.2.1',

                // Support library version
                // https://developer.android.com/topic/libraries/support-library/revisions.html
                'support_lib'        : '28.0.0',

                // Constraint Layout
                // https://developer.android.com/training/constraint-layout/index.html
                'constraint_layout'  : '1.1.3',

                // Android Architecture Components
                // https://developer.android.com/topic/libraries/architecture/adding-components.html
                'lifecycle'          : '1.1.1',

                // Retrofit 2
                // http://square.github.io/retrofit/
                'retrofit'           : '2.4.0',

                // Picasso
                // http://square.github.io/picasso/
                'picasso'            : '2.71828',

                // OkHTTP
                // https://github.com/square/okhttp
                'ok_http'            : '3.11.0',
                // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
                // OkHttp interceptor
                'ok_http_interceptor': '3.11.0',

                // Fabric - Crashlytics lib
                // https://fabric.io/kits/android/crashlytics/install
                'crashlytics'        : '2.9.5',

                // Dagger 2
                // https://github.com/google/dagger
                // See version at,
                // https://mvnrepository.com/artifact/com.google.dagger/dagger
                // https://mvnrepository.com/artifact/com.google.dagger/dagger-android
                // https://mvnrepository.com/artifact/com.google.dagger/dagger-android-support
                // https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler
                'dagger_2'           : '2.17',

                // DaggerMock
                // https://github.com/fabioCollini/DaggerMock
                'dagger_mock'        : '0.8.4',

                // Kotlin coroutines
                // https://github.com/Kotlin/kotlinx.coroutines
                // https://mvnrepository.com/artifact/org.jetbrains.kotlinx/kotlinx-coroutines-android
                'kotlin_coroutines'  : '0.30.1',

                // RxAndroid
                // https://github.com/ReactiveX/RxAndroid
                'rx_android'         : '2.1.0',
                'rx_java'            : '2.2.2',

                // https://github.com/sheungon/AndroidEasyLog
                'android_ec_log'     : '0.2.1',

                // protobuf
                // https://proandroiddev.com/how-to-setup-your-android-app-to-use-protobuf-96132340de5c
                // https://mvnrepository.com/artifact/com.google.protobuf/protoc
                'protoc'             : '3.5.1-1',
                // https://repo1.maven.org/maven2/com/google/protobuf/protoc-gen-javalite/
                'protoc_gen_javalite': '3.0.0',
                // https://github.com/google/protobuf-gradle-plugin
                'protobuf_plugin'    : '0.8.6',

                /* SignalR
                 * https://github.com/SignalR/java-client */
                // https://bintray.com/jtribe/maven/signalr-client-sdk
                // TODO update signalR lib
                //'signalr_client_sdk'        : '1.0.8',
                // https://bintray.com/jtribe/maven/signalr-client-sdk-android
                //'signalr_client_sdk_android': '1.0.0',

                // https://github.com/Bilibili/ijkplayer
                'ijkplayer'          : '0.8.8',

                // http://square.github.io/otto/
                'otto'               : '1.3.8',

                // https://github.com/google/gson
                'gson'               : '2.8.5',

                // https://developer.android.com/topic/libraries/testing-support-library/packages.html
                'espresso'           : '3.0.2',

                // Mockito
                // http://site.mockito.org/
                // https://mvnrepository.com/artifact/org.mockito/mockito-android
                'mockito'            : '2.18.3',

                // PowerMock
                // https://github.com/powermock/powermock
                'power_mock'         : '1.7.4',
        ]

        deps = [
                // http://junit.org/junit4/
                'junit': 'junit:junit:4.12'
        ]

    }
    repositories {
        google()
        jcenter()

        // For signal R
        mavenCentral()
        // For Fabric
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_plugin"

        /*
        * For Fabric
        * https://s3.amazonaws.com/fabric-artifacts/public/io/fabric/tools/gradle/maven-metadata.xml
        * */
        classpath 'io.fabric.tools:gradle:1.26.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath "com.google.protobuf:protobuf-gradle-plugin:$versions.protobuf_plugin"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        // For signal R
        mavenCentral()
        maven { url "https://dl.bintray.com/jtribe/maven" }

        // For Fabric
        maven { url 'https://maven.fabric.io/public' }

        /*
         * For PhotoView, DaggerMock
         * https://github.com/chrisbanes/PhotoView
         */
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    /*
    * Fix dependency error
    * https://github.com/JakeWharton/u2020/blob/9870fa4291f5c813ed929aec26c94bee2b92d2b7/build.gradle#L105-L113
    * */
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support'
                        && details.requested.name != 'multidex'
                        && details.requested.name != 'multidex-instrumentation') {
                    details.useVersion "$versions.support_lib"
                } else if (details.requested.group == 'android.arch.lifecycle') {
                    details.useVersion "$versions.lifecycle"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
